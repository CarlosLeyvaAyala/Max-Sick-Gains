Scriptname Maxick_Utils Hidden

; Path to mod configuration folder.
string Function cfgDir() Global
  return "Data/SKSE/Plugins/Maxick/"
EndFunction

;@Deprecated: No longer neeeded
; Path to configuration file generated by _Max Sick Gains.exe_.
string Function genCfg() Global
  return cfgDir() + "genCfg.json"
EndFunction

;@Deprecated: No longer neeeded
; Path to female sliders file.
string Function femSliders() Global
  return cfgDir() + "fem-sliders.json"
EndFunction

;@Deprecated: No longer neeeded
; Path to male sliders file.
string Function manSliders() Global
  return cfgDir() + "man-sliders.json"
EndFunction

;@Deprecated: No longer neeeded
string Function widgetFile() Global
  return cfgDir() + "widget.json"
EndFunction

bool Function PapyrusUtilExists() Global
  Return PapyrusUtil.GetVersion() > 1
EndFunction

bool Function SexLabExists() Global
  Return game.IsPluginInstalled("SexLab.esm")
EndFunction

bool Function NiOverrideExists() Global
  Return NiOverride.GetScriptVersion() > 0 && SKSE.GetPluginVersion("skee") >= 1
EndFunction

bool Function JContainersExists() Global
  If !JContainers.isInstalled()
    Debug.MessageBox("Max Sick Gains: JContainers is a hard requirement and you did not install it. Read the requirements carefully.")
    return false
  EndIf
  Return true
EndFunction

; Tries to return a handle to iWantWidgets. Shows a message if failed to do so.
iwant_widgets Function iWantWidgetsHandle() Global
  iwant_widgets iWidgets = Game.GetFormFromFile(0x000800, "iWant Widgets.esl") as iwant_widgets

  If !iWidgets
    iWidgets = Game.GetFormFromFile(0x000800, "iWant Widgets.esp") as iwant_widgets
    If !iWidgets
      Debug.MessageBox("Max Sick Gains: iWant Widgets is a hard requirement and you did not install it. Read the requirements carefully.")
    EndIf
  EndIf
  return iWidgets
EndFunction
